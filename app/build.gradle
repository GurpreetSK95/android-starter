apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.gurpreetsk.android_starter"
        minSdkVersion 19
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 1
        versionName "0.0.1"

        vectorDrawables.useSupportLibrary = true

        buildConfigField 'int', 'DB_VERSION', '1'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['room.schemaLocation': "$projectDir/db-schemas".toString()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'DB_NAME', "\"starter-db-debug\""
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'DB_NAME', "\"starter-db\""
        }
    }

    sourceSets {
        main.java.srcDirs        += 'src/main/kotlin'
        debug.java.srcDirs       += 'src/debug/kotlin'
        test.java.srcDirs        += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android KTX
    implementation 'androidx.core:core-ktx:1.0.1'

    // Coroutines
    // TODO: Remove if not needed.
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1"

    // Dagger 2
    implementation "com.google.dagger:dagger:2.16"
    kapt "com.google.dagger:dagger-compiler:2.16"

    // Kotlin stdlib
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.6.0"
    implementation "com.squareup.moshi:moshi-adapters:1.6.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.6.0"

    // okHttp
    implementation "com.squareup.okhttp3:okhttp:3.12.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.11.0"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.5.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.5.0"

    // Reactive Extensions
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"
    implementation "io.reactivex.rxjava2:rxjava:2.2.2"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "com.jakewharton.rxrelay2:rxrelay:2.1.0"

    // Room
    implementation "androidx.room:room-runtime:2.0.0"
    implementation "androidx.room:room-rxjava2:2.0.0"
    kapt "androidx.room:room-compiler:2.0.0"
    testImplementation "androidx.room:room-testing:2.0.0"

    // Stetho
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    // Support
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.vectordrawable:vectordrawable:1.0.1"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"

    /* -----  Testing  ----- */

    // JUnit testing
    testImplementation "junit:junit:4.12"
    testImplementation "com.google.truth:truth:0.42"
    testImplementation "org.robolectric:robolectric:3.6.1"

    // Mocking
    testImplementation "org.mockito:mockito-core:2.23.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"
    androidTestImplementation "org.mockito:mockito-android:2.21.0"

    // Android test
    androidTestImplementation "com.google.truth:truth:0.42"
    androidTestImplementation "androidx.test:runner:1.1.1"
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.1"
}
